``` VB6
'GitHub Account: GitHub.com/AliRezaJoodi

Dim CPN As Byte
Dim i As Byte
Dim buf As Variant
Dim counter As Variant
Dim Status_relay As Byte

Private Sub Form_Load()
    MSComm1.Settings = "9600,N,8,1"
    MSComm1.RThreshold = 1
    MSComm1.SThreshold = 1
    MSComm1.InputLen = 1

    Combo1.Clear
    For CPN = 1 To 16
        Combo1.AddItem Str(CPN)
    Next CPN
    Combo1.Text = Combo1.List(0)
    
    Command_Stop.Enabled = False
    For i = 0 To 9
        Command_Relay(i).Enabled = False
    Next i
    
    For i = 0 To 7
        Display_Relay(i).BackColor = RGB(150, 150, 150)
    Next i
End Sub

Private Sub Command_Start_Click()
    Command_Start.Enabled = False
    Command_Stop.Enabled = True
    Combo1.Enabled = False
    CPN = Val(Combo1.Text)
    MSComm1.CommPort = CPN
    MSComm1.PortOpen = True
    For i = 0 To 9
        Command_Relay(i).Enabled = True
    Next i
End Sub

Private Sub Command_Stop_Click()
    Command_Stop.Enabled = False
    Command_Start.Enabled = True
    MSComm1.PortOpen = False
    Combo1.Enabled = True
    For i = 0 To 9
        Command_Relay(i).Enabled = False
    Next i
    For i = 0 To 7
        Display_Relay(i).BackColor = RGB(150, 150, 150)
    Next i
End Sub

Private Sub Command_Relay_Click(Index As Integer)
    MSComm1.Output = Chr(97 + Index) + Chr(13)
End Sub

Private Sub MSComm1_OnComm()
    Select Case MSComm1.CommEvent
        Case comEvReceive
            buf = MSComm1.Input
            buf = Asc(buf)
            If buf = &HEB And counter = 0 Then
                counter = 1
                Exit Sub
            End If
            If counter = 1 Then
                Status_relay = buf
                For i = 0 To 7
                    If (Status_relay And (2 ^ i)) >= 1 Then
                        Display_Relay(i).BackColor = RGB(250, 0, 0)
                    Else
                        Display_Relay(i).BackColor = RGB(150, 150, 150)
                    End If
                Next i
                counter = 0
            End If
    End Select
End Sub

```
